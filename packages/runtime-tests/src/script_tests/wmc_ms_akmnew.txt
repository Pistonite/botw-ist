// reference: https://www.youtube.com/watch?v=EwSSDGqQ1tY
// kinak made the setup
get slate
get 3 shroom 6 pepper
get pot-lid[dura=5] tree-branch axe[dura=40] trav-bow 3 arrow 5 bomb-arrow
get 4 orb glider
# kak
get phrenic nightshade
eat all but 2 shroom all pepper
get 2 fairy nightshade 3 endura-carrot wood
shoot 2 times # 1 arrow left
# korok forest
get forest-dweller-shield
equip bomb-arrow
shoot 3 times
drop wood
get master-sword
get 2 silent-princess
shoot
sell all orb
save # auto
# make slots
:smug hold 
  shroom nightshade fairy endura-carrot princess
sell all materials
pick-up fairy endura-carrot shroom
drop shroom
get toasty-hylian-shroom
reload
drop endura-carrot[slot=2] nightshade
:smug hold
  princess shroom nightshade fairy[slot=2]
sell princess shroom fairy[held]
pick-up fairy
:dpad equip master-sword
# pe
entangle slate drop pot-lid
entangle glider drop shield[slot=2]
entangle pot-lid equip tree-branch
drop all shield all bow
unpause drop
use weapon 4 times
drop tree-branch
drop endura-carrot # roast
pick-up endura-carrot
!remove all endura-carrot
get wild-green[effect=endura]
pick-up nightshade
get roasted-endura-carrot
entangle roasted-endura-carrot
hold 2 fairies drop
pick-up roasted-endura-carrot
entangle roasted-endura-carrot
hold 3 fairy nightshade
sort material # will put you on fairy
hold fairy
drop # cook
get wild-green[effect=sneaky, price=311, hp=112]
# corruption
pick-up phrenic
entangle food
:targeting <_>[category=bow]
eat toasty-hylian-shroom
equip axe
pick-up roasted-endura-carrot
save
pick-up all shield
unequip shield 
drop bow
unequip weapon
eat all material all food
reload; save
drop bow unpause
entangle food
:targeting <_>[category=bow]
eat toasty-hylian-shroom
unpause entangle food
:targeting <_>[category=bow]
eat toasty-hylian-shroom
pick-up bow
unequip weapon
equip dweller-shield
eat all material all food
reload
shoot
save
drop all axe all bow
eat all wild-green roasted-endura-carrot
reload
drop axe bow
eat wild-green
reload
eat all food[value=1]
sell wild-green
# bol
save # castle
# ganon fight
!write [dura=40] to master-sword
equip master-sword
shoot 18 times # blights & ganon
get bow-of-light
equip bow-of-light
# after final shot
equip phrenic
reload
sort bow
drop all shield
save
pick-up 3 shields
reload
